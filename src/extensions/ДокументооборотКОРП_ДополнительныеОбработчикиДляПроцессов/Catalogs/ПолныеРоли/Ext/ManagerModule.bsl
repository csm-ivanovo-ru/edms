#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Находит или создает полную роль по исходной роли, дополненной объектами адресации.
//
// Параметры:
//  ИсходнаяРоль		- СправочникСсылка.РолиИсполнителей	 - исходная роль.
//  ОбъектыАдресации	- Структура, ФиксированнаяСтруктура	 - структура с реквизитами для
//  	поиска / создания полной роли.
//  ПроверятьНаличиеИсполнителейРоли					 - Булево	- генерирует исключение,
//  	если для вычисленной роли нет исполнителей.
//  ИскатьИсполнителейРолиДляВышестоящихПодразделений	 - Булево	- если ОбъектыАдресации
//  	содержат Подразделение, тода данный реквизит активирует механизм проверки наличия исполнителей
//  	полной роли. Если исполнители не заданы, тогда осуществляется попытка поиска полной роли для
//  	вышестоящего подразделения.
// 
// Возвращаемое значение:
//  СправочникСсылка.ПолныеРоли - полная роль, найденная в справочнике или созданная на
//  основании исходной роли.
//
Функция НайтиСоздатьПолнуюРольПоОбъектамАдресации(Знач ИсходнаяРоль,
	Знач ОбъектыАдресации = Неопределено,
	Знач ПроверятьНаличиеИсполнителейРоли = Ложь,
	Знач ИскатьИсполнителейРолиДляВышестоящихПодразделений = Ложь) Экспорт
	
	Контекст = "Справочники.ПолныеРоли.НайтиСоздатьПолнуюРольПоОбъектамАдресации";
	
	ПолнаяРоль = Неопределено;
	
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(
		Контекст,
		"ИсходнаяРоль", ИсходнаяРоль,
		Тип("СправочникСсылка.РолиИсполнителей"));
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(
		Контекст,
		"ИскатьИсполнителейРолиДляВышестоящихПодразделений", ИскатьИсполнителейРолиДляВышестоящихПодразделений,
		Тип("Булево"));
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(
		Контекст,
		"ПроверятьНаличиеИсполнителейРоли", ПроверятьНаличиеИсполнителейРоли,
		Тип("Булево"));
	ОбщегоНазначенияКлиентСервер.Проверить(
		Не (ИскатьИсполнителейРолиДляВышестоящихПодразделений И
			Не ПроверятьНаличиеИсполнителейРоли),
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр(
				"ru = 'Поиск исполнителей роли для вышестоящих подразделений возможен только с проверкой наличия исполнителей роли ""%1""'",
				ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
			Строка(ИсходнаяРоль)),
		Контекст);
	ОбщегоНазначенияКлиентСервер.Проверить(
		ИсходнаяРоль.ИспользуетсяБезОбъектовАдресации Или ЗначениеЗаполнено(ОбъектыАдресации),
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр(
				"ru = 'Роль ""%1"" не используется без объектов адресации. Объекты адресации должны быть указаны'",
				ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
			Строка(ИсходнаяРоль)),
		Контекст);
	ОбщегоНазначенияКлиентСервер.Проверить(
		Не ИскатьИсполнителейРолиДляВышестоящихПодразделений
			Или ОбъектыАдресации.Свойство(ПланыВидовХарактеристик.ОбъектыАдресацииЗадач.Подразделение.Наименование),
		НСтр(
			"ru = 'Указана необходимость поиска исполнителей ролей для вышестоящих подразделений, однако в объектах адресации не указано подразделение'",
			ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
		Контекст);
		
	ПолнаяРоль =  НайтиСоздатьПолнуюРольПоОбъектамАдресацииВспомогательная(ИсходнаяРоль, ОбъектыАдресации);
	
	Если Не ПроверятьНаличиеИсполнителейРоли Тогда 
		
		Возврат ПолнаяРоль;
		
	ИначеЕсли ИсходнаяРоль.ИспользуетсяСОбъектамиАдресации И ИскатьИсполнителейРолиДляВышестоящихПодразделений Тогда
		
		Если БизнесПроцессыИЗадачиСервер.ЕстьИсполнителиРоли(ПолнаяРоль) Тогда 
			Возврат ПолнаяРоль;
		КонецЕсли;
		
		НаименованиеОбъектаАдресацииПодразделение = ПланыВидовХарактеристик.ОбъектыАдресацииЗадач.Подразделение.Наименование;
		Подразделение = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ОбъектыАдресации, НаименованиеОбъектаАдресацииПодразделение);
		ВышестоящиеПодразделения = ОбщегоНазначенияДокументооборот.ВсеРодителиЭлемента(Подразделение);
		НовыеОбъектыАдресации = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ОбъектыАдресации);
		
		Для Каждого Подразделение Из ВышестоящиеПодразделения Цикл
			НовыеОбъектыАдресации.Вставить(НаименованиеОбъектаАдресацииПодразделение, Подразделение);
			ПолнаяРоль =  НайтиСоздатьПолнуюРольПоОбъектамАдресацииВспомогательная(ИсходнаяРоль, НовыеОбъектыАдресации);
			Если БизнесПроцессыИЗадачиСервер.ЕстьИсполнителиРоли(ПолнаяРоль) Тогда
				Возврат ПолнаяРоль;
			КонецЕсли;
		КонецЦикла;
		
		Если ИсходнаяРоль.ИспользуетсяБезОбъектовАдресации Тогда
			ПолнаяРоль = НайтиСоздатьПолнуюРоль(ИсходнаяРоль);
			Если БизнесПроцессыИЗадачиСервер.ЕстьИсполнителиРоли(ПолнаяРоль) Тогда
				Возврат ПолнаяРоль;
			КонецЕсли;
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.Проверить(
			Ложь,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр(
					"ru = 'Вычисление полной роли невозможно. Не указаны исполнители роли ""%1"" для подразделения ""%2"" и вышестоящих подразделений'",
					ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
				Строка(ИсходнаяРоль), Строка(ОбъектыАдресации.Подразделение)),
			Контекст);
		
	Иначе
		
		ОбщегоНазначенияКлиентСервер.Проверить(
			БизнесПроцессыИЗадачиСервер.ЕстьИсполнителиРоли(ПолнаяРоль),
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр(
					"ru = 'Вычисление полной роли невозможно. Не указаны исполнители роли ""%1"" (полная роль ""%2"")'",
					ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
				Строка(ИсходнаяРоль), Строка(ПолнаяРоль)),
			Контекст);
		Возврат ПолнаяРоль;
		
	КонецЕсли;
		
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Находит или создает полную роль по исходной роли, дополненной объектами адресации.
//
// Параметры:
//  ИсходнаяРоль		- СправочникСсылка.РолиИсполнителей	 - исходная роль.
//  ОбъектыАдресации	- Структура, ФиксированнаяСтруктура	 - структура с реквизитами для
//  	поиска / создания полной роли.
// 
// Возвращаемое значение:
//  СправочникСсылка.ПолныеРоли - полная роль, найденная в справочнике или созданная на
//  основании исходной роли.
//
Функция НайтиСоздатьПолнуюРольПоОбъектамАдресацииВспомогательная(Знач ИсходнаяРоль, Знач ОбъектыАдресации)
	
	Контекст = "Справочники.ПолныеРоли.НайтиСоздатьПолнуюРольПоОбъектамАдресацииВспомогательная";
	
	ПолнаяРоль = Неопределено;
	Если ИсходнаяРоль.ИспользуетсяСОбъектамиАдресации И ЗначениеЗаполнено(ОбъектыАдресации) Тогда
		ОсновнойОбъектАдресации = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ОбъектыАдресации,
			ИсходнаяРоль.ТипыОсновногоОбъектаАдресации.Наименование);
		ДополнительныйОбъектАдресации = Неопределено;
		Если ЗначениеЗаполнено(ИсходнаяРоль.ТипыДополнительногоОбъектаАдресации) Тогда 
			ДополнительныйОбъектАдресации = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ОбъектыАдресации,
				ИсходнаяРоль.ТипыДополнительногоОбъектаАдресации.Наименование);
		КонецЕсли;
		ПолнаяРоль = НайтиСоздатьПолнуюРоль(ИсходнаяРоль,
			ОсновнойОбъектАдресации,
			ДополнительныйОбъектАдресации);
	ИначеЕсли ИсходнаяРоль.ИспользуетсяБезОбъектовАдресации Тогда
		ПолнаяРоль = НайтиСоздатьПолнуюРоль(ИсходнаяРоль);
	КонецЕсли;
	
	Возврат ПолнаяРоль;
		
КонецФункции

#КонецОбласти

#КонецЕсли