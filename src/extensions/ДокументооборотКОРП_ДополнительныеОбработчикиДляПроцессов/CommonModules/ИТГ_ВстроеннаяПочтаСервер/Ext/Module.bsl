////////////////////////////////////////////////////////////////////////////////
// РАБОТА СО ВСТРОЕННОЙ ПОЧТОЙ

#Область ПрограммныйИнтерфейс

// Возвращает ссылка на исходное входящее письмо, которое переслано Письмом
//  или Неопределено
//
// Параметры:
//  Письмо	 - ДокументСсылка.ВходящееПисьмо - ссылка на входящее письмо
// 
// Возвращаемое значение:
//  ДокументСсылка.ВходящееПисьмо - ссылка на исходное входящее письмо,
//  которое переслано Письмом
//
Функция ПолучитьВходящееПисьмоОснование(Знач Письмо) Экспорт
		
	Контекст = "ИТГ_ВстроеннаяПочтаСервер.ПолучитьВходящееПисьмоОснование";
	
	ОбщегоНазначенияКлиентСервер.Проверить(
		ВстроеннаяПочтаКлиентСервер.ЭтоВходящееПисьмо(Письмо),
		НСтр("ru = 'Недопустимое значение параметра Письмо, 
			|Ожидалась ссылка на входящее письмо'"),
		Контекст);
		
	Основание = Неопределено;
	
	Заголовок = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Письмо, "ВнутреннийЗаголовок");

	ИдентификаторПисьма = ВстроеннаяПочтаСервер.ПолучитьЗначениеПоляИзЗаголовкаПисьма(Заголовок, "In-Reply-To");
	Если ЗначениеЗаполнено(ИдентификаторПисьма) Тогда
		ИдентификаторОснования = РаботаСоСтроками.ВыделитьПодстрокуВСкобках(ИдентификаторПисьма, "<", ">");
		Если ИдентификаторОснования <> "NULL" Тогда 
			Основание = ВстроеннаяПочтаСервер.НайтиВходящееПисьмоПоИдентификатору(ИдентификаторОснования);
		КонецЕсли;	
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Основание) Тогда // не было поля "In-Reply-To" или не нашли по идентификатору
		СтрокаИдентификаторовПисем = ВстроеннаяПочтаСервер.ПолучитьЗначениеПоляИзЗаголовкаПисьма(Заголовок, "References");
		Если ЗначениеЗаполнено(СтрокаИдентификаторовПисем) Тогда
			МассивИдентификаторовПисем = ВстроеннаяПочтаСервер.ПолучитьМассивИдентификаторовПисем(СтрокаИдентификаторовПисем);
			Если ЗначениеЗаполнено(МассивИдентификаторовПисем) Тогда
				ИдентификаторОснования = МассивИдентификаторовПисем[0];
				Основание = ВстроеннаяПочтаСервер.НайтиВходящееПисьмоПоИдентификатору(ИдентификаторОснования);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Основание;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьМассивИдентификаторовПисем(Знач СтрокаИдентификаторовПисем)
	
	Результат = Новый Массив;
	
	СтрокаИдентификаторовПисем = СтрЗаменить(СтрокаИдентификаторовПисем, "> <", Символы.ПС);
	СтрокаИдентификаторовПисем = СтрЗаменить(СтрокаИдентификаторовПисем, "<", "");
	СтрокаИдентификаторовПисем = СтрЗаменить(СтрокаИдентификаторовПисем, ">", "");
	
	Для Индекс = 1 По СтрДлина(СтрокаИдентификаторовПисем) Цикл
		Строка = СтрПолучитьСтроку(СтрокаИдентификаторовПисем, Индекс);
		Если ЗначениеЗаполнено(Строка) Тогда
			ИдентификаторУрезанный = Лев(СокрЛП(Строка), 200);	
			Результат.Добавить(ИдентификаторУрезанный);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
