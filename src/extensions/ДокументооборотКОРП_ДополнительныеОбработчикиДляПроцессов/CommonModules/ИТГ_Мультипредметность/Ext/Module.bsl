////////////////////////////////////////////////////////////////////////////////
// Подсистема "Мультипредметность"
// Модуль Мультипредметность: сервер, внешнее соединение
//
// Содержит процедуры и функции обработки объектов и механизмов, 
// поддерживающих мультипредметность.
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Помечает основной предмет процесса на удаление
//
// Параметры:
//  ПроцессСсылка	 - БизнесПроцессСсылка	 - ссылка на процесс, предмет которого следует пометить на удаление.
//  Основные		 - Булево	 - пометить только основные предметы процесса.
//
Процедура УстановитьПометкуУдаленияДляПредметовПроцесса(Знач ПроцессСсылка, Знач Основные = Неопределено) Экспорт
	
	Контекст = "ИТГ_Мультипредметность.УстановитьПометкуУдаленияДляПредметовПроцесса";
	
	// TODO: обеспечить возможность обработки не только ссылки на процесс, но и объекта процесса
	
	ПредметыПроцесса = Мультипредметность.ПолучитьПредметыПроцесса(ПроцессСсылка, Основные);
	
	Для Каждого СтрокаПредмета Из ПредметыПроцесса Цикл
		
		ОбщегоНазначенияКлиентСервер.Проверить(
			ЗначениеЗаполнено(СтрокаПредмета.Предмет),
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр(
					"ru = 'Операция не может быть выполнена, так как не указан предмет ""%1"" процесса'",
					ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
				Строка(СтрокаПредмета.ИмяПредмета)),
			Контекст);
			
		Делопроизводство.УстановитьПометкуУдаления(СтрокаПредмета.Предмет, Истина);
		
	КонецЦикла;
	
КонецПроцедуры

// Устанавливает состояние основного предмета процесса
//
// Параметры:
//  ПроцессСсылка	 - БизнесПроцессСсылка	 - ссылка на процесс, предмет которого следует пометить на удаление.
//  Основные		 - Булево	 - пометить только основные предметы процесса.
//  Состояние		 - Перечисления.СостоянияДокументов	 - состояние документа, которое необходимо установить.
//
Процедура УстановитьСостояниеДляПредметовПроцесса(Знач ПроцессСсылка, Знач Основные = Истина, Знач Состояние) Экспорт
	
	Контекст = "ИТГ_Мультипредметность.УстановитьСостояниеДляПредметовПроцесса";
	
	// TODO: обеспечить возможность обработки не только ссылки на процесс, но и объекта процесса
	
	ПредметыПроцесса = Мультипредметность.ПолучитьПредметыПроцесса(ПроцессСсылка, Основные);
	
	Для Каждого СтрокаПредмета Из ПредметыПроцесса Цикл
		
		ОбщегоНазначенияКлиентСервер.Проверить(
			ЗначениеЗаполнено(СтрокаПредмета.Предмет),
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр(
					"ru = 'Операция не может быть выполнена, так как не указан предмет ""%1"" процесса'",
					ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
				Строка(СтрокаПредмета.ИмяПредмета)),
			Контекст);
		
		ДокументСсылка = СтрокаПредмета.Предмет;
		
		ОбщегоНазначенияКлиентСервер.Проверить(
			ДелопроизводствоКлиентСервер.ЭтоСсылкаНаДокумент(ДокументСсылка),
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр(
					"ru = 'Операция не может быть выполнена, так как предмет ""%1"" процесса не является документом'",
					ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
				Строка(СтрокаПредмета.ИмяПредмета)),
			Контекст);

		Делопроизводство.ЗаписатьСостояниеДокумента(ДокументСсылка, ТекущаяДата(), Состояние, ПроцессСсылка);
			
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
