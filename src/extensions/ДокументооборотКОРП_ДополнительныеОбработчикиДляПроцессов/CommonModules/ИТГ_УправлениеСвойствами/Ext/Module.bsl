////////////////////////////////////////////////////////////////////////////////
// Подсистема "Свойства"
// 
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает значения дополнительного свойства объекта "Важность".
//
// Параметры:
//  ВладелецСвойств	 - Ссылка	 - например, внутренний документ.
// 
// Возвращаемое значение:
//   - СправочникСсылка.ЗначенияСвойствОбъектов.
//
Функция ПолучитьЗначениеСвойства_Важность(Знач ВладелецСвойств) Экспорт
	
	ОбычнаяВажность = Справочники.ЗначенияСвойствОбъектов.НайтиСоздатьЗначениеСвойства_Важность_Обычная();
	
	Если УправлениеСвойствами.ИспользоватьДопРеквизиты(ВладелецСвойств) Тогда
		
		Свойство = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиСоздатьСвойство_Важность();
		
		Если УправлениеСвойствами.ПроверитьСвойствоУОбъекта(ВладелецСвойств, Свойство) Тогда 
			
			ЗначенияСвойств = УправлениеСвойствами.ПолучитьЗначенияСвойств(ВладелецСвойств, Истина, Ложь, Свойство);
			
			Если ЗначенияСвойств.Количество() > 0 Тогда 
				Возврат ЗначенияСвойств.Получить(0).Значение;
			Иначе
				Возврат ОбычнаяВажность;
			КонецЕсли;
			
		Иначе 
			Возврат ОбычнаяВажность;
		КонецЕсли;
		
	Иначе 
		Возврат ОбычнаяВажность;
	КонецЕсли;
	
КонецФункции
	
// Возвращает значения дополнительного свойства объекта "Должность работника".
//
// Параметры:
//  ВладелецСвойств	 - Ссылка	 - например, внутренний документ.
// 
// Возвращаемое значение:
//   - СправочникСсылка.Должности.
//
Функция ПолучитьЗначениеСвойства_ДолжностьРаботника(Знач ВладелецСвойств) Экспорт
	
	Результат = Справочники.Должности.ПустаяСсылка();
	
	Если УправлениеСвойствами.ИспользоватьДопРеквизиты(ВладелецСвойств) Тогда
		
		Свойство = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиСоздатьСвойство_ДолжностьРаботника();
		
		Если УправлениеСвойствами.ПроверитьСвойствоУОбъекта(ВладелецСвойств, Свойство) Тогда 
			ЗначенияСвойств = УправлениеСвойствами.ПолучитьЗначенияСвойств(ВладелецСвойств, Истина, Ложь, Свойство);
			Если ЗначенияСвойств.Количество() > 0 Тогда 
				Результат = ЗначенияСвойств.Получить(0).Значение;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции
	
#КонецОбласти
