////////////////////////////////////////////////////////////////////////////////
// Подсистема "Свойства"
// 
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает значения дополнительного свойства объекта "Важность".
//
// Параметры:
//  ВладелецСвойств	 - Ссылка	 - например, внутренний документ.
// 
// Возвращаемое значение:
//   - СправочникСсылка.ЗначенияСвойствОбъектов.
//
Функция ПолучитьЗначениеСвойства_Важность(Знач ВладелецСвойств) Экспорт
	
	Возврат ПолучитьЗначениеСвойства(ВладелецСвойств,
		ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиСоздатьСвойство_Важность(),
		Справочники.ЗначенияСвойствОбъектов.НайтиСоздатьЗначениеСвойства_Важность_Обычная());
		
КонецФункции
	
// Возвращает значения дополнительного свойства объекта "Должность работника".
//
// Параметры:
//  ВладелецСвойств	 - Ссылка	 - например, внутренний документ.
// 
// Возвращаемое значение:
//   - СправочникСсылка.Должности.
//
Функция ПолучитьЗначениеСвойства_ДолжностьРаботника(Знач ВладелецСвойств) Экспорт
	
	Возврат ПолучитьЗначениеСвойства(ВладелецСвойств,
		ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиСоздатьСвойство_ДолжностьРаботника(),
		Справочники.Должности.ПустаяСсылка());
	
КонецФункции
	
// Возвращает значения дополнительного свойства объекта "Подразделение работника".
//
// Параметры:
//  ВладелецСвойств	 - Ссылка	 - например, внутренний документ.
// 
// Возвращаемое значение:
//   - СправочникСсылка.СтруктураПредприятия.
//
Функция ПолучитьЗначениеСвойства_ПодразделениеРаботника(Знач ВладелецСвойств) Экспорт
	
	Возврат ПолучитьЗначениеСвойства(ВладелецСвойств,
		ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиСоздатьСвойство_ПодразделениеРаботника(),
		Справочники.СтруктураПредприятия.ПустаяСсылка());
	
КонецФункции
	
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает единичное значение указанного дополнительного свойства объекта.
//
// Параметры:
//  ВладелецСвойств	 	- Ссылка	 - например, внутренний документ.
//  Свойство		 	- ПланыВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения	 - свойство.
//  ЗначениеПоУмолчанию	- значение, возвращаемое, если свойство не установлено.
// 
// Возвращаемое значение:
//   - значение свойства.
//
Функция ПолучитьЗначениеСвойства(Знач ВладелецСвойств, Знач Свойство, Знач ЗначениеПоУмолчанию = Неопределено)
	
	Контекст = "ИТГ_УправлениеСвойствами.ПолучитьЗначениеСвойства";
	
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(
		Контекст,
		"Свойство",
		Свойство,
		Тип("ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения"));
		
	Результат = ЗначениеПоУмолчанию;
	
	Если УправлениеСвойствами.ИспользоватьДопРеквизиты(ВладелецСвойств) Тогда
		
		Если УправлениеСвойствами.ПроверитьСвойствоУОбъекта(ВладелецСвойств, Свойство) Тогда 
			ЗначенияСвойств = УправлениеСвойствами.ПолучитьЗначенияСвойств(ВладелецСвойств, Истина, Ложь, Свойство);
			Если ЗначенияСвойств.Количество() > 0 Тогда 
				Результат = ЗначенияСвойств.Получить(0).Значение;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции
	
#КонецОбласти
