///////////////////////////////////////////////////////////////////////////////////////////////
// МОДУЛЬ СОДЕРЖИТ ПРОЦЕДУРЫ И ФУНКЦИИ РАБОТЫ С ВНУТРЕННИМИ, ВХОДЯЩИМИ И ИСХОДЯЩИМИ ДОКУМЕНТАМИ
// 

#Область ПрограммныйИнтерфейс

// Проверяет, находится ли документ на исполнении (на исполнении либо исполнен,
//  на рассмотрении либо рассмотрен, на ознакомлении)
//
// Параметры:
//  Документ - СправочникСсылка	 - ссылка на внутренний либо входящий документ
// 
// Возвращаемое значение:
//  Булево - Истина, если документ на исполнении, Ложь - если нет
//
Функция ПолучитьСостояниеДокументаНаИсполнении(Знач Документ) Экспорт
		
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСостоянияДокументов") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Документ) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	МассивСостояний = Делопроизводство.ПолучитьВсеСостоянияДокумента(Документ).ВыгрузитьКолонку("Состояние");
		
	Типы = Новый Структура(
		"СостояниеРассмотрение,
		|СостояниеИсполнение");
	СоответствиеСостояний = Делопроизводство.ПолучитьСоответствиеСостояний();
	
	Для Каждого Тип Из Типы Цикл
		Для Каждого Состояние Из МассивСостояний Цикл
			Если СоответствиеСостояний.Получить(Состояние) = Тип.Ключ Тогда
				Возврат Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти
