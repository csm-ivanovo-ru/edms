///////////////////////////////////////////////////////////////////////////////////////////////
// МОДУЛЬ СОДЕРЖИТ ПРОЦЕДУРЫ И ФУНКЦИИ РАБОТЫ С ПОДПИСЯМИ
// 

#Область ПрограммныйИнтерфейс

// Обрабатывает внутренний документ, для которого используется учёт сторон:
//  если состояние документа - подписан, а в табличной части Стороны сведения иные,
//  устанавливат состояние Подписан в табличной части Стороны датой регистрации
//  документа.
//
// Параметры:
//  Документ - СправочникОбъект.ВнутренниеДокументы	 - обрабатываемый документ
//
Процедура ЗаполнитьСостояниеПодписейВСторонахПоСостояниюДокумента(Знач ДокументОбъект) Экспорт
	
	Контекст = "ИТГ_РаботаСПодписямиДокументов.ЗаполнитьСостояниеПодписейВСторонахПоСостояниюДокумента";
	
	ИспользоватьСостоянияДокументов = ПолучитьФункциональнуюОпцию("ИспользоватьСостоянияДокументов");
	Если Не ИспользоватьСостоянияДокументов Тогда
		Возврат;
	КонецЕсли;
	
	ИспользоватьВидыВнутреннихДокументов = ПолучитьФункциональнуюОпцию("ИспользоватьВидыВнутреннихДокументов");
	Если Не ИспользоватьВидыВнутреннихДокументов Тогда
		Возврат;
	КонецЕсли;
		
	ОбщегоНазначенияКлиентСервер.Проверить(
		ДелопроизводствоКлиентСервер.ЭтоВнутреннийДокумент(ДокументОбъект),
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Неверное значение параметра Документ. 
				|Ожидалась ссылка на внутренний документ.
				|Полученное значение имеет тип ""%1""'",
				ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
			Метаданные.НайтиПоТипу(ТипЗнч(ДокументОбъект)).ПолноеИмя()),
		Контекст);
	
	РеквизитыВидаДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументОбъект.ВидДокумента,
		"ВариантПодписания, ВестиУчетСторон");
		
	Если РеквизитыВидаДокумента.ВестиУчетСторон Тогда
		
		ДокументПодписан = РаботаСПодписямиДокументов.ДокументПодписанСторонами(ДокументОбъект.Стороны, РеквизитыВидаДокумента.ВариантПодписания);
		СостояниеДокумента = Делопроизводство.ПолучитьСостояниеДокумента(ДокументОбъект.Ссылка, "СостояниеПодписание");
		
		Если Не ДокументПодписан Тогда 
			Если СостояниеДокумента = Перечисления.СостоянияДокументов.Подписан Тогда 
				
				Попытка
					
					Для Каждого СтрокаСтороны Из ДокументОбъект.Стороны Цикл
						Если РаботаСПодписямиДокументовКлиентСервер.ЭтоОрганизация(СтрокаСтороны.Сторона)
							И Не СтрокаСтороны.Подписан Тогда
								// Не меняем состояния подписи для организации
						ИначеЕсли РаботаСПодписямиДокументовКлиентСервер.ЭтоКонтрагент(СтрокаСтороны.Сторона)
							И Не СтрокаСтороны.Подписан Тогда
								СтрокаСтороны.Подписан = Истина;
								СтрокаСтороны.ДатаПодписи = ДокументОбъект.ДатаРегистрации;
						КонецЕсли;
					КонецЦикла;
					
					ДокументОбъект.Записать();
					
					ЗаписьЖурналаРегистрации("РаботаСПодписямиДокументов.ЗаполнитьСостояниеПодписейВСторонахПоСостояниюДокумента",
						УровеньЖурналаРегистрации.Предупреждение, , ,
						СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Сведения о подписи в табличной части Стороны
								|внутреннего документа ""%2"" не соответствуют состоянию документа ""%3"". 
								|Сведения о подписании документа установлены в табличной части Стороны. 
								|Контекст ""%1""'",
								ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
							Контекст, Строка(ДокументОбъект), Строка(СостояниеДокумента)));
						
				Исключение
					
					ЗаписьЖурналаРегистрации("РаботаСПодписямиДокументов.ЗаполнитьСостояниеПодписейВСторонахПоСостояниюДокумента",
						УровеньЖурналаРегистрации.Предупреждение, , ,
						СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Сведения о подписи в табличной части Стороны
								|внутреннего документа ""%2"" не соответствуют состоянию документа ""%3"". 
								|При изменении сведений о подписании документа в табличной части Стороны возникла ошибка. 
								|Контекст ""%1""'",
								ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
							Контекст, Строка(ДокументОбъект), Строка(СостояниеДокумента)));
					ВызватьИсключение;
						
				КонецПопытки;
					
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
		
КонецПроцедуры	

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти
