#Если НЕ ВебКлиент Тогда
		
////////////////////////////////////////////////////
//// РАБОТА С OPEN OFFICE

//Выполняет замену данных в документе OpenOffice Writer
//Параметры:СтарыйПутьКФайлу - путь на диске, по которому выгружена текущая версия файла
//			ТекущаяВерсияРасширение - расширение файла
//			Файл - ссылка на объект файл, в котором следует выполнить замену
//			МассивДанныхДляАвтоЗамен - массив настроек для заполнения файла данными
//Возвращает:Путь на диске к заполненному файлу
&Вместо("ЗаполнитьФайлOpenOfficeWriter")
Функция ИТГ_ЗаполнитьФайлOpenOfficeWriter(ДвоичныеДанныеФайла, ТекущаяВерсияРасширение, МассивДанныхДляАвтоЗамен)
	
	СтарыйПутьКФайлу = ПолучитьИмяВременногоФайла(ТекущаяВерсияРасширение);
	ДвоичныеДанныеФайла.Записать(СтарыйПутьКФайлу);
	НовыйПутьКФайлу = ПолучитьИмяВременногоФайла(ТекущаяВерсияРасширение);
	
	КопироватьФайл(СтарыйПутьКФайлу, СтрЗаменить(СтарыйПутьКФайлу, ТекущаяВерсияРасширение, "zip"));
	ИмяФайлаСПутемZIP = СтрЗаменить(СтарыйПутьКФайлу, ТекущаяВерсияРасширение, "zip");

	ВременнаяПапкаДляРазархивирования = ПолучитьИмяВременногоФайла("");
	ВременныйZIPФайл = ПолучитьИмяВременногоФайла("zip"); 

	Архив = Новый ЧтениеZipФайла();
	Архив.Открыть(ИмяФайлаСПутемZIP);
	Архив.ИзвлечьВсе(ВременнаяПапкаДляРазархивирования, РежимВосстановленияПутейФайловZIP.Восстанавливать);
	Архив.Закрыть();

	//Заполнение полей в теле документа
	ЧтениеXML = Новый ЧтениеXML();
	ЧтениеXML.ОткрытьФайл(ВременнаяПапкаДляРазархивирования + "/content.xml");
	ЗаписьXML = Новый ЗаписьXML;
	// ИТГ: исправляем проблемы с пробелами после автозаполнения
	ПараметрыЗаписи = Новый ПараметрыЗаписиXML( , , Ложь, Ложь);
	// ЗаписьXML.ОткрытьФайл(ВременнаяПапкаДляРазархивирования + "/content_update.xml");
	ЗаписьXML.ОткрытьФайл(ВременнаяПапкаДляРазархивирования + "/content_update.xml", ПараметрыЗаписи);
	// ИТГ: конец исправления проблем с пробелами после автозаполнения
	ЗаписьXML.ЗаписатьОбъявлениеXML();
		
	ВыполнитьЗаменуПолейИСтрокВДокументеOpenOfficeWriter(ЧтениеXML, ЗаписьXML, МассивДанныхДляАвтоЗамен);
	ЧтениеXML.Закрыть();
	ЗаписьXML.Закрыть();

	ПереместитьФайл(ВременнаяПапкаДляРазархивирования + "/content_update.xml", ВременнаяПапкаДляРазархивирования + "/content.xml");
	УдалитьФайлы(ВременнаяПапкаДляРазархивирования + "/content_update.xml");

	//заполнение полей в колонтитулах
	ЧтениеXML = Новый ЧтениеXML();
	ЧтениеXML.ОткрытьФайл(ВременнаяПапкаДляРазархивирования + "/styles.xml");
	ЗаписьXML = Новый ЗаписьXML;
	// ИТГ: исправляем проблемы с пробелами после автозаполнения
	// ЗаписьXML.ОткрытьФайл(ВременнаяПапкаДляРазархивирования + "/styles_update.xml");
	ЗаписьXML.ОткрытьФайл(ВременнаяПапкаДляРазархивирования + "/styles_update.xml", ПараметрыЗаписи);
	// ИТГ: конец исправления проблем с пробелами после автозаполнения
	ЗаписьXML.ЗаписатьОбъявлениеXML(); 

	ВыполнитьЗаменуПолейИСтрокВДокументеOpenOfficeWriter(ЧтениеXML, ЗаписьXML, МассивДанныхДляАвтоЗамен);
	
	ЧтениеXML.Закрыть();
	ЗаписьXML.Закрыть();
	
	ПереместитьФайл(ВременнаяПапкаДляРазархивирования + "/styles_update.xml", ВременнаяПапкаДляРазархивирования + "/styles.xml");
	УдалитьФайлы(ВременнаяПапкаДляРазархивирования + "/styles_update.xml");
	
	Архиватор = Новый ЗаписьZipФайла(ВременныйZIPФайл, "", "");
	Архиватор.Добавить(ВременнаяПапкаДляРазархивирования + "\*.*", РежимСохраненияПутейZIP.СохранятьОтносительныеПути, РежимОбработкиПодкаталоговZIP.ОбрабатыватьРекурсивно);
	Архиватор.Записать();

	ПереместитьФайл(ВременныйZIPФайл, НовыйПутьКФайлу);
	УдалитьФайлы(ВременнаяПапкаДляРазархивирования);	
	
	ДвоичныеДанныеЗаполненногоФайла = Новый ДвоичныеДанные(НовыйПутьКФайлу);
	УдалитьФайлы(СтарыйПутьКФайлу);
	УдалитьФайлы(НовыйПутьКФайлу);
	
	Возврат ДвоичныеДанныеЗаполненногоФайла;
	
КонецФункции

#КонецЕсли
